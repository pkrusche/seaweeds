###############################################################################
# Copyright (C) 2007   Peter Krusche, The University of Warwick         
# peter@dcs.warwick.ac.uk                                
###############################################################################

import os
import os.path
import glob
import string
import re
import sys
import platform;

from SCons.Defaults import *

###############################################################################
# read options and configure directories
###############################################################################

optfilename = 'opts.py'

# Try to find options file based on hostname
optfilename_local = 'opts_'+platform.uname()[1]+'_'+platform.uname()[0]+'_'+platform.uname()[4]+'.py'
if len(glob.glob(optfilename_local)) > 0:
    optfilename = optfilename_local
else:
    print 'To use specific options for this system, use options file "'+optfilename_local+'"'

print 'Using options from ' + optfilename

opts = Variables(optfilename)

# these are the options that can be specified through the command line
opts.AddVariables(
	EnumVariable('mode', 'Build mode: set to debug or release', 'debug',
                    allowed_values = ('debug', 'release'),
                    ignorecase = 1),
	EnumVariable('simd_mode', 
				 'Vector instruction set to use: mmx, sse2, or none', 'sse2',
                    allowed_values = ('mmx', 'sse2', 'nommx'),
                    ignorecase = 1),
	BoolVariable('tests', 'Run tests', 0),
	BoolVariable('benchmarks', 'Run benchmarks', 0),
	BoolVariable('tuning', 'Run tuning', 0),
	BoolVariable('profile', 'Enable profiling. Also enables debug information.', 0),
	BoolVariable('debuginfo', 'Include debug information also in release version.', 1),
	BoolVariable('docs', 'Build documentation', 0),
	BoolVariable('configure', 'Perform configuration', 0),
	BoolVariable('use_yasm', 'Use yasm instead of nasm', 1),
	BoolVariable('use_ati_brook', 'Use ATI brook+', 0),
	BoolVariable('sequential', 'Use sequential bsponmpi (no MPI)', 0),
    ('win32_boostdir', 'Path to Boost library in Win32', 'C:\\Boost\\include'),
    ('win32_ccpdir', 'Path to Microsoft Compute Cluster Pack in Win32', 'C:\\Program Files\\Microsoft Compute Cluster Pack'),
    ('win32_tbbdir', 'Path to tbb library in Win32', 'C:\\tbb'),
    ('win32_bsponmpidir', 'Path to bsponmpi library in Win32', 'C:\\bsponmpi'),
    ('BROOKROOT', 'Path to ATI Stream SDK', ''),
	('toolset', 'Specify compiler and linker tools: gcc|default', 'default'),
	('additional_lflags', 'Additional linker flags', ''),
	('additional_cflags', 'Additional compiler flags', ''),
	('MPICC', 'MPI c compiler wrapper (Unix only)', 'mpicc'),
	('MPICXX', 'MPI c++ compiler wrapper (Unix only)', 'mpicxx'),
	('MPILINK', 'MPI linker wrapper (Unix only)', 'mpicxx'),
	('replacement_CC', 'Replacement for the standard C compiler', ''),
	('replacement_CXX', 'Replacement for the standard C++ compiler', ''),
	('replacement_LIB', 'Replacement for the standard lib tool', ''),
	('replacement_LINK', 'Replacement for the standard linker ', ''),
	)

SCons.Defaults.DefaultEnvironment(tools = [])

# read options before creating root environment
readopts = Environment(tools = [], options = opts)
use_ati = readopts['use_ati_brook']
if int(readopts['tests']):
	print "Will run tests."
if int(readopts['benchmarks']):
	print "Will run benchmarks."

###############################################################################
# Set up the root environment
###############################################################################

# add qt4 here if necessary
platform_name = platform.uname()[0]
if platform_name == 'Windows':
	if readopts['toolset'] == 'gcc':
		ttools = ['gnulink', 'nasm', 'gcc', 'g++', 'ar', 'doxygen']
	else:
		ttools = ['msvc', 'mslib', 'mslink', 'nasm', 'doxygen']
elif platform_name == 'Linux':
	if readopts['toolset'] == 'icc':
		ttools = ['ar', 'link', 'nasm', 'gcc', 'g++', 'intelc', 'doxygen']
	else:
		ttools = ['gnulink', 'nasm', 'gcc', 'g++', 'ar', 'doxygen']
else:
	ttools = ['default', 'nasm', 'doxygen']

if use_ati:
	ttools.append('ati')

root = Environment(
    tools = ttools,
    toolpath = ['site_scons/site_tools'],
    options = opts,
)
Help(opts.GenerateHelpText(root))

###############################################################################
# Setup compiling parameters
###############################################################################

root.Append(
	ENV = os.environ,
	BINDIR = "#bin",
	LIBDIR = "#lib",
	SRCDIR = "#src",
	CPATH = ['#src'],
	CPPPATH = ['#src'],
	)

# dependency optimization
root.SetOption('max_drift', 4)
root.SetOption('implicit_cache', 1)
root.SetOption('diskcheck', None)
root.Ignore('', '')

subarch = platform.uname()[4]
bitness = platform.architecture()[0]
platform_name = ARGUMENTS.get('OS', root['PLATFORM'])
if platform_name == 'darwin' and bitness == '64bit':
	print "MacOS X: using 64 bit mode"
	subarch = 'x86_64'
else:
	print "We are on platform " + platform_name + " " + platform.architecture()[0] + " " + subarch


mode = root['mode']
toolset = root['toolset']
profile = root['profile']
sequential = root['sequential']
debuginfo = root['debuginfo']
arch_id = str(Platform()) + '_' + toolset + '_' + mode
root.Prepend(PROGSUFFIX = "_"+arch_id) 

win32_boostdir = root['win32_boostdir']
win32_tbbdir = root['win32_tbbdir']
win32_ccpdir = root['win32_ccpdir']
win32_bsponmpidir = root['win32_bsponmpidir']

cxx = str(root.subst('$CXX'))
print "Compiling on platform "+ platform_name + " using mode " + mode + " and compiler " + cxx + "."

root.Append(
    CCFLAGS = ' $CFLAGS',
)

if platform_name == 'win32':
    # Win32 specific setup
      
    boost_include = ''
    boost_lib = ''
    # see if we can find boost
    dirs = glob.glob(win32_boostdir+"\\*")
    if len(dirs) > 0:
    	dirs.sort()
    	boost_include = dirs[len(dirs) - 1]
    	boost_lib = win32_boostdir+"\\..\\lib"
    
    # boost include path must go into CCFLAGS because the scons include dependency
    # parser can die otherwise
    if cxx != 'g++' and readopts['toolset'] != 'gcc':
		root.Append(
			CCFLAGS = ' /D_SCL_SECURE_NO_WARNINGS /I'+boost_include ,
			LIBPATH = [ boost_lib ],
		)

		if subarch == 'AMD64':
			root.Append(
				ARFLAGS = '/MACHINE:X64',
				LINKFLAGS = '/MACHINE:X64',
			)
		else:
			root.Append(
				ARFLAGS = '/MACHINE:X86',
				LINKFLAGS = '/MACHINE:X86',
			)

    else:
		root.Append(
			CCFLAGS = ' -I'+boost_include ,
			LIBPATH = [ boost_lib ],
		)
    
    
    # see if we can find tbb
    root.Append(
        CPPPATH = [ win32_tbbdir+'\\include' ],
        # we require that the right libraries are copied here, since it's a lot of 
        # work to guess the right system and folder.
        LIBPATH = [ win32_tbbdir+'\\lib' ],
    )   

    # see if we can find bsponmpi
    root.Append(
        CPPPATH = [ win32_bsponmpidir +'\\include' ],
        LIBPATH = [ win32_bsponmpidir +'\\lib' ],
    )
else:
		# Unix-like stuff
		root.Append(
			CCFLAGS = ' $CFLAGS',
			LIBPATH = ['.'],
		)

root.Append(
	CCFLAGS = root['additional_cflags'],
	LINKFLAGS = root['additional_lflags'],
	)

###############################################################################
# Setup debug / release mode flags
###############################################################################

if string.find(cxx, 'g++') >= 0 or string.find(cxx, 'c++') >= 0 or root['toolset'] == 'gcc':
	if mode == 'debug':
		as_debugformat = 'dwarf2'
		if subarch == 'x86_64':
			as_debugformat = 'null'
		root.Append(
		CCFLAGS=' -g -O0',
		ASFLAGS=' -g '+as_debugformat+' ',
		)
	elif mode == 'release':
		if debuginfo:
			root.Append(
				CCFLAGS=' -g ',
				LINKFLAGS=' -g ',
			)
		if profile:
			root.Append(
				CCFLAGS=' -pg -O3',
				LINKFLAGS=' -pg',
			)
		else:
			root.Append(
			CCFLAGS=' -O3',
			)
		
	print "using g++"
elif root['toolset'] == 'icc' and platform_name != 'win32':
	if mode == 'debug':
		as_debugformat = 'dwarf2'
		if subarch == 'x86_64':
			as_debugformat = 'null'
		root.Append(
		CCFLAGS=' -g -O0',
		ASFLAGS=' -g '+as_debugformat+' ',
		)
	elif mode == 'release':
		if debuginfo:
			root.Append(
				CCFLAGS=' -g ',
				LINKFLAGS=' -g ',
			)
		if profile:
			root.Append(
				CCFLAGS=' -pg -O3 -ipo',
				LINKFLAGS=' -pg',
			)
		else:
			root.Append(
			CCFLAGS=' -O3 -fast -ipo',
			)
		
	print "using icc"
elif platform_name == 'win32':
	msvc_ccflags =  " /EHsc /nologo /wd4099 /D_CRT_SECURE_NO_DEPRECATE /WL /Zi"
	msvc_linkflags = " /DEBUG"
	if mode == 'debug':
		root.Append(
		CCFLAGS='/MDd /Od /Zi /W3 /RTC1 /RTCu /RTCs'+msvc_ccflags,
		LINKFLAGS='/DEBUG '+msvc_linkflags,
		)
	elif mode == 'release':
		root.Append( 
		CCFLAGS='/MD '+msvc_ccflags,
		LINKFLAGS=msvc_linkflags
		)
		if debuginfo or profile:
			root.Append(
				CCFLAGS=' /Zi ',
				LINKFLAGS=' /DEBUG '
			)							
	print "using MSVC"
		
if root['replacement_CC']:
	root.Replace(
		CC=root['replacement_CC']
	)
if root['replacement_CXX']:
	root.Replace(
		CXX=root['replacement_CXX']
	)
if root['replacement_LIB']:
	root.Replace(
		AR=root['replacement_LIB']
	)
if root['replacement_LINK']:
	root.Replace(
		LINK=root['replacement_LINK']
	)

###############################################################################
# Setup nasm/yasm object file types
###############################################################################

# see if we use yasm
if int(root['use_yasm']):
	root.Replace(AS = 'yasm')
	
if platform_name == 'win32':
	if readopts['toolset'] != 'gcc':
		if subarch == 'AMD64':
			root.Append(ASFLAGS = " -f win64 ")
		else:
			root.Append(ASFLAGS = " -f win32 ")
	else:
		if subarch == 'AMD64':
			root.Append(ASFLAGS = " -f coff ")
		else:
			root.Append(ASFLAGS = " -f coff ")		
	
	if mode == 'debug' or profile:
		root.Append(ASFLAGS = " -g cv8")
elif platform_name == 'darwin':
	if subarch == 'x86_64':
		root.Append(ASFLAGS = " -f macho64 ")	
	else:
		root.Append(ASFLAGS = " -f macho ")	
else:
    if subarch == 'x86_64':
         root.Append(ASFLAGS = " -f elf64 ")
    else:
         root.Append(ASFLAGS = " -f elf ")

###############################################################################
# Setup TBB library linking
###############################################################################

if mode=='debug':
	root.Append(LIBS = ['tbb_debug'])
else:
	root.Append(LIBS = ['tbb'])

###############################################################################
# Setup automatic parameter tuning
###############################################################################

def tuning_builder(target, source, env):
	tuning_config_h_begin = """
/*
 * This file is generated automatically and will be overwritten.
 */ 
#ifndef __TUNING_CONFIG_H__
#define __TUNING_CONFIG_H__

"""
	
	tuning_config_h_end = """

#endif /* __TUNING_CONFIG_H__ */

"""
	
	if int(env['tuning']):
	#	env.Command('tuning_'+arch_id+'.dat', '#bin/rangetuning', "bin/rangetuning > $TARGET")
		tuneresults = ""
	
		# print summary and write to file
		print "Flags added by tuning phase: \n" + tuneresults
		f = open(str(target[0]), 'w')
		f.write(tuning_config_h_begin);
		f.write(tuneresults);
		f.write(tuning_config_h_end);
	else:
		if not os.path.exists(str(target[0])):
			f = open(str(target[0]), 'w')
			f.write(tuning_config_h_begin);
			f.write(tuning_config_h_end);

if int(root['tuning']):
	bld = Builder(action = tuning_builder,
		          suffix = '.h',
			      src_suffix = '')
	root.Append(
		BUILDERS = { "Tuning" : bld }
	)
	print "Will perform tuning."

###############################################################################
# Automatic configuration code
###############################################################################

spawnp_test_cpp = """

#include <spawn.h>

#define P_WAIT 0

extern char **environ;

int main(int argc, char** argv) {
	__pid_t temp;

	posix_spawnp(&temp, "ls", NULL, NULL, NULL, environ));
	waitpid(temp, NULL, 0);
	return 0;
}

"""

def CheckSpawnp(context):
    context.Message('Checking if system supports posix spawn...')
    result = context.TryLink(spawnp_test_cpp, '.cpp')
    context.Result(result)
    return result


# configure environment
if root['configure'] != 0:
	print "Performing autoconfiguration"
	autoconfig_h_begin = """
/*
 * This file is generated automatically and will be overwritten.
 */ 
#ifndef __AUTOCONFIG_H__
#define __AUTOCONFIG_H__

#ifdef _MSC_VER
// Turn off global optimizations in MSVC, they FUCK THINGS UP!
//#pragma optimize("g",off)

#else
#define __cdecl
#endif

#ifndef _WIN32
#include <typeinfo>

#include <boost/cstdint.hpp>

typedef boost::int32_t INT32;
typedef boost::uint32_t UINT32;
typedef boost::int64_t INT64;
typedef boost::uint64_t UINT64;

typedef boost::uint8_t BYTE;
typedef boost::uint32_t DWORD;
typedef boost::uint16_t WORD;
#else
#include <Windows.h>
#endif

#ifdef _DEBUG
#include <assert.h>
#define ASSERT assert
#else
#define ASSERT(x)
#endif

"""
	
	autoconfig_h_end = """

#endif /* __AUTOCONFIG_H__ */

"""	
	conf = Configure(root, custom_tests = {'CheckSpawnp' : CheckSpawnp})
	autohdr = open("src/autoconfig.h", 'w')
	autohdr.write(autoconfig_h_begin);

	if subarch == 'x86_64' or subarch == 'AMD64':
		autohdr.write("#define _X86_64 \n")
		
	if root['simd_mode'] != 'nommx':
		autohdr.write("#define _HAVE_SIMD \n")

	if root['simd_mode'] == 'sse2':
		autohdr.write("#define _HAVE_SSE2 \n")

	if conf.CheckSpawnp():
		autohdr.write("#define _POSIX_SPAWNP \n")
	
	if use_ati:
		autohdr.write("#define _HAVE_ATI_GPU \n")	

	autohdr.write("#define MEMCPY memcpy \n")	

	autohdr.write(autoconfig_h_end)
	root = conf.Finish()
	autohdr.close()

if int(root['tuning']):
	arch_id = arch_id + "_tuned"

###############################################################################
# Setup library suffix
###############################################################################

libsuffix = ''
if sequential == 1:
	libsuffix += 'sequential'
if mode == 'debug':
	libsuffix += '_debug'

###############################################################################
# The library of assembler optimized integer operations
###############################################################################

vec_machineword_asm_suffix = root['simd_mode']
if subarch == 'AMD64':
	print "using assembler code for " + subarch
	xasmlib_files = ['src/xasmlib/machineword_AMD64.asm', 'src/xasmlib/machineword_AMD64_'+vec_machineword_asm_suffix+'.asm', 'src/xasmlib/xasmlib.c']	
elif subarch == 'x86_64':
	print "using assembler code for " + subarch
	xasmlib_files = ['src/xasmlib/machineword_x86_64.asm', 'src/xasmlib/machineword_x86_64_'+vec_machineword_asm_suffix+'.asm', 'src/xasmlib/xasmlib.c']
else:
	print "using assembler code for " + subarch
	xasmlib_files = ['src/xasmlib/machineword_x86.asm', 'src/xasmlib/machineword_x86_'+vec_machineword_asm_suffix+'.asm', 'src/xasmlib/xasmlib.c']
	
xasmlib = root.Library('lib/xasmlib'+libsuffix, xasmlib_files)
root.Append(LIBS = xasmlib)

###############################################################################
# BSP C++ utility library
###############################################################################

bspcpplib_files = ['src/bspcpp/timing.cpp', 'src/bspcpp/ParameterFile.cpp', 'src/bspcpp/CommandLine.cpp', 'src/bspcpp/singletons.cpp']
bspcpplib = root.Library('lib/bspcpp'+libsuffix, bspcpplib_files)
root.Append(LIBS = bspcpplib)

###############################################################################
# ATI GPU accellerated code
###############################################################################

if use_ati:
	root.Append(
		CPPPATH = '$BROOK_CPPPATH', 
		LIBPATH = '$BROOK_LIBPATH', 
		LIBS = ['brook'],
	)	
		
	gpulib_files = [ 
		'src/gpulib/seaweeds.cpp',
		root.BrCC('src/gpulib/seaweeds_gpu.br'), 
	]
	gpulib = root.Library('lib/gpulib'+libsuffix, gpulib_files);
	root.Append(LIBS = gpulib)

###############################################################################
# Setup MPI capable environment
###############################################################################

mpi = root.Clone()

if sequential:
	root.Append(LIBS = [
		"bsponmpi"+libsuffix]
		)
else:
	root.Append(LIBS = [
		"bsponmpisequential"+libsuffix]
		)

if not sequential:
	if platform_name == 'win32':
		win32_ccpdir = root['win32_ccpdir']
		mpi.Append(	CPPPATH = win32_ccpdir+"\\Include",
				LIBS = ["msmpi.lib", "msmpe.lib"]
		  )
		if subarch == 'AMD64':
			mpi.Append(	LIBPATH = win32_ccpdir+"\\Lib\\amd64" )
		else:
			mpi.Append(	LIBPATH = win32_ccpdir+"\\Lib\\i386" )
	else:
		mpi.Replace(
			CXX = root['MPICXX'],
			LINK = root['MPILINK'],
			CC = root['MPICC']
		)
	mpi.Append (
		CPPDEFINES = "_HAVE_MPI",
		LIBS = ["bsponmpi"+libsuffix]
	)
else:
	mpi.Append (
		CPPDEFINES = "_SEQUENTIAL",
		LIBS = ["bsponmpi"+libsuffix]
	)

###############################################################################
# Setup automatic parameter tuning
###############################################################################

nontuned = root.Clone()
nontuned.Append(CCFLAGS = " -D_TUNING ")

rangetuning_objects = nontuned.Object('src/tuning/rangetuning.cpp')
Ignore(rangetuning_objects, 'tuning_config.h')

nontuned.Program('bin/rangetuning', rangetuning_objects)

#root.Tuning('tuning_config.h', [
#	'bin/rangetuning'+dict['PROGSUFFIX'],
#	])

###############################################################################
# Export our build environments for the SConscripts
###############################################################################

Export(['root', 'mpi', 'mode', 'arch_id'])

###############################################################################
# get SConscripts
###############################################################################

SConscript('src/SConscript')
SConscript('src/tests/SConscript')
